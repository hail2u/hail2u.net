# Blosxom Plugin: permalink
# Author(s): Kyo Nagashima <kyo@hail2u.net>
# Version: 2016-05-08
# Blosxom Home/Docs/Licensing: http://www.blosxom.com/

package permalink;

use strict;
use vars qw($enabled $title $title_sep);

# --- Configurable variables -----------

# separator string between $blog_title and $title
my $str_title_sep = " - ";

# --- Plug-in package variables --------

$enabled = 0;
my $description_placeholder = '{{{permalink::description}}}';
my $image_placeholder = '{{{permalink::image}}}';
my $title_placeholder = '{{{permalink::title}}}';
my $d;
my $i = $blosxom::blog_image;
my $t;

# --------------------------------------

sub start {
  if ($blosxom::path_info !~ /\.\Q$blosxom::flavour\E$/) {
    return 0;
  }

  $enabled = 1;
  $title = $title_placeholder;
  $title_sep = $str_title_sep;
  $blosxom::blog_description = $description_placeholder;
  $blosxom::blog_image = $image_placeholder;

  return 1;
}

sub story {
  my($pkg, $path, $filename, $story_ref, $title_ref, $body_ref) = @_;

  $d = $$body_ref;
  $d =~ tr!\x0D\x0A!!d;
  $d =~ s!^.*?<p.*?>(.*?)</p>.*?$!$1!;
  $d =~ s!<.*?>!!g;
  $d =~ s!"!&quot;!g;

  if ($$body_ref =~ m!<img\s[^>]*src="(.*?)"!) {
    $i = $1;
  }

  $t = $$title_ref;
  $t =~ s!"!&quot;!g;

  return 1;
}

sub last {
  $blosxom::output =~ s/$description_placeholder/$d/gm;
  $blosxom::output =~ s/$image_placeholder/$i/gm;
  $blosxom::output =~ s/$title_placeholder/$t/gm;
}

1;
# vim:ft=perl:
